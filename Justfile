# üöÄ Terraform Module Development Workflow: Automate setup, formatting, linting, and project management

# üêö Shell configuration
# Use bash with strict error handling to prevent silent failures
# -u: Treat unset variables as an error
# -e: Exit immediately if a command exits with a non-zero status
set shell := ["bash", "-uce"]
set dotenv-load

# Avoid reporting traces to Dagger
# TODO: Uncomment if traces aren't needed.
# export NOTHANKS := "1"

# üéØ Default task: Display available recipes when no specific task is specified
default: help

# üì¶ Variables for project directories
TESTS_DIR := 'tests/modules'
MODULES_DIR := 'modules'
EXAMPLES_DIR := 'examples'
FIXTURES_DIR := 'fixtures'

# ‚ÑπÔ∏è List all available recipes with their descriptions
help:
    @just --list

# üîß Install pre-commit hooks in Nix environment for consistent code quality
hooks-install-nix:
    @echo "üß∞ Installing pre-commit hooks in Nix environment..."
    @nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command pre-commit install

# üîß Install pre-commit hooks in local environment for code consistency
hooks-install:
    @echo "üß∞ Installing pre-commit hooks locally..."
    @./scripts/hooks/pre-commit-init.sh init

# üïµÔ∏è Run pre-commit hooks across all files in Nix environment
hooks-run-nix:
    @echo "üîç Running pre-commit hooks from .pre-commit-config.yaml in Nix environment..."
    @nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command pre-commit run --all-files

# üïµÔ∏è Run pre-commit hooks across all files in local environment
hooks-run:
    @echo "üîç Running pre-commit hooks from .pre-commit-config.yaml..."
    @./scripts/hooks/pre-commit-init.sh run

# üîç Check if a module is a Terraform module
is-tf-module MOD='default':
    @echo "üîç Checking if module: {{MODULES_DIR}}/{{MOD}} is a Terraform module..."
    @if [ -z "$(find "{{MODULES_DIR}}/{{MOD}}" -type f -name '*.tf')" ]; then \
        echo "‚ùå No Terraform files found in module: {{MODULES_DIR}}/{{MOD}}"; \
        exit 1; \
    fi

# üßπ Remove Terraform and Terragrunt cache directories to reset project state
clean-tf:
    @echo "üóëÔ∏è Cleaning Terraform and Terragrunt cache directories across the entire repository..."
    find . -type d -name ".terraform" -exec rm -rf {} +; \
    find . -type d -name ".terragrunt-cache" -exec rm -rf {} +; \
    find . -type f -name "*.tfstate" -exec rm -f {} +; \
    find . -type f -name "*.tfstate.backup" -exec rm -f {} +; \
    echo "‚úÖ Cleanup complete!"

# üßπ Remove Terraform and Terragrunt cache directories for a specific module
clean-tf-mod MOD='default': (is-tf-module MOD)
    @echo "üóëÔ∏è Cleaning Terraform and Terragrunt cache directories for module: {{MOD}}..."
    @echo "üîç Found module: {{MODULES_DIR}}/{{MOD}}"
    @echo "üìÇ Listing directories and files in module: {{MODULES_DIR}}/{{MOD}}"
    @ls -R "{{MODULES_DIR}}/{{MOD}}"
    find "{{MODULES_DIR}}/{{MOD}}" -type d -name ".terraform" -exec rm -rf {} +; \
    find "{{MODULES_DIR}}/{{MOD}}" -type d -name ".terragrunt-cache" -exec rm -rf {} +; \
    find "{{MODULES_DIR}}/{{MOD}}" -type f -name "*.tfstate" -exec rm -f {} +; \
    find "{{MODULES_DIR}}/{{MOD}}" -type f -name "*.tfstate.backup" -exec rm -f {} +; \
    echo "‚úÖ Cleanup complete!"

# üßπ Comprehensive cleanup of project artifacts, state files, and cache directories
clean:
    @echo "üóëÔ∏è Performing comprehensive project cleanup for general purposes..."
    @find . -name ".DS_Store" -exec rm -f {} +
    @find . -name "*.log" -exec rm -f {} +

# üßπ Comprehensive cleanup of project artifacts, state files, and cache directories in Nix environment
clean-all: clean clean-tf

# üßê Format YAML files using yamlfmt in Nix environment
yaml-fix-nix:
    @echo "üîß Formatting YAML files with yamlfmt in Nix environment..."
    @nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command yamlfmt .

# üßπ Format and lint YAML files for consistency and quality
yaml-fix:
    @echo "üîß Formatting and linting YAML files..."
    @yamlfmt .
    @echo "üïµÔ∏è Validating YAML configuration..."
    @yamllint --config-file .yamllint.yml --strict .
    @echo "‚úÖ YAML formatting and linting complete!"

# üïµÔ∏è Lint YAML files using yamllint in Nix environment
yaml-lint-nix:
    @echo "üïµÔ∏è Linting YAML files in Nix environment..."
    @nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command yamllint .

# üïµÔ∏è Validate YAML files against strict configuration standards
yaml-lint:
    @echo "üïµÔ∏è Linting YAML files..."
    @yamlfmt .
    @echo "üïµÔ∏è Checking yamllint configuration..."
    @yamllint --config-file .yamllint.yml --strict .
    @echo "‚úÖ YAML formatting and linting complete!"

# üêö Lint shell scripts using shellcheck in Nix environment
scripts-lint-nix:
    @echo "üêö Linting shell scripts in Nix environment..."
    @nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command bash -c 'find . -type f -name "*.sh" | xargs shellcheck'

# üêö Perform static analysis on all shell scripts
scripts-lint:
    @echo "üêö Linting shell scripts..."
    @find . -type f -name "*.sh" | xargs shellcheck

# ü¶´ Lint Go files using custom script in Nix environment
go-lint-nix:
    @echo "ü¶´ Linting Go files in Nix environment..."
    @nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command bash -c 'cd tests/ && go mod tidy && golangci-lint run --verbose --config ../.golangci.yml'

# ü¶´ Perform static code analysis on Go files
go-lint:
    @echo "ü¶´ Linting Go files..."
    @cd tests/ && go mod tidy && golangci-lint run --verbose --config ../.golangci.yml

# üêπ Format Go files in Nix environment using gofmt
go-format-nix:
    @echo "üêπ Formatting Go files in Nix environment..."
    @nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command bash -c 'find . -type f -name "*.go" -not -path "*/vendor/*" -exec gofmt -w {} +'

# üêπ Format Go files locally within the tests directory
go-format:
    @echo "üêπ Formatting Go files in tests directory..."
    @cd tests && \
    echo "üìã Go files to be formatted:" && \
    find . -type f -name "*.go" -not -path "*/vendor/*" | tee /dev/tty | xargs gofmt -w

# üêπ Tidy Go files in Nix environment
go-tidy-nix:
    @echo "üêπ Tidying Go files in Nix environment..."
    @nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command bash -c 'cd tests/ && go mod tidy'

# üêπ Tidy Go files locally within the tests directory
go-tidy:
    @echo "üêπ Tidying Go files in tests directory..."
    @cd tests && go mod tidy

# üêπ Comprehensive CI checks for Go files
go-ci: (go-tidy) (go-format) (go-lint)
    @echo "‚úÖ Go files CI checks completed"

# üêπ Comprehensive CI checks for Go files in Nix environment
go-ci-nix: (go-tidy-nix) (go-format-nix) (go-lint-nix)
    @echo "‚úÖ Go files CI checks completed in Nix environment"

# üöÄ Launch Nix development shell with project dependencies
dev:
    @echo "üåø Starting Nix Development Shell for Terraform Registry Module Template üè∑Ô∏è"
    @nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes

# üîì Enable direnv for environment variable management
allow-direnv:
    @echo "üîì Allowing direnv in the current directory..."
    @direnv allow

# üîÑ Reload direnv environment configuration
reload-direnv:
    @echo "üîÅ Reloading direnv environment..."
    @direnv reload

# üåø Format Terraform files locally using terraform fmt
tf-format:
    @echo "üé® Formatting Terraform files in current directory..."
    @./scripts/utilities/format.sh --terraform

# üåø Format all Terraform files across modules, examples, and tests directories
tf-format-all:
    @echo "üé® Formatting all Terraform files across repository..."
    @./scripts/utilities/format.sh --terraform --tf-all-dirs

# üåø Format Terraform files for a specific module (both module and example directories)
tf-format-module MODULE:
    @echo "üé® Formatting Terraform files for module: {{MODULE}}"
    @./scripts/utilities/format.sh --terraform --tf-module {{MODULE}}

# üîç Check Terraform file formatting without modifying files
tf-format-check MOD='':
    @echo "üîç Checking Terraform file formatting..."
    @if [ -z "{{MOD}}" ]; then \
        ./scripts/utilities/format.sh --terraform --tf-check; \
    else \
        ./scripts/utilities/format.sh --terraform --tf-check --tf-module {{MOD}}; \
    fi

# üîç Check Terraform file formatting for all directories
tf-format-check-all:
    @echo "üîç Checking Terraform file formatting across all directories..."
    @./scripts/utilities/format.sh --terraform --tf-check --tf-all-dirs

# üîç Check Terraform file formatting for a specific module
tf-format-check-module MODULE:
    @echo "üîç Checking Terraform file formatting for module: {{MODULE}}"
    @./scripts/utilities/format.sh --terraform --tf-check --tf-module {{MODULE}}

# üîç Check Terraform file formatting without modifying files (Nix environment)
tf-format-check-nix MOD='':
    @echo "üîç Checking Terraform file formatting in Nix environment..."
    @if [ -z "{{MOD}}" ]; then \
        nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command ./scripts/utilities/format.sh --terraform --tf-check; \
    else \
        nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command ./scripts/utilities/format.sh --terraform --tf-check --tf-module {{MOD}}; \
    fi

# üîç Discover and list all Terraform files in the repository
tf-discover:
    @echo "üîç Discovering Terraform files..."
    @./scripts/utilities/format.sh --terraform --tf-discover

# üåø Run Terraform commands with flexible working directory and command selection
tf-exec WORKDIR='.' CMDS='--help':
    @echo "üèóÔ∏è Running Terraform command:"
    @echo "üë®üèª‚Äçüíª Command: terraform {{CMDS}}"
    @echo "üìÇ Working directory: $(realpath {{WORKDIR}})"
    @cd "{{WORKDIR}}" && terraform {{CMDS}}

# üåø Run Terraform commands in Nix development environment with flexible working directory and command selection
tf-exec-nix WORKDIR='.' CMDS='--help':
    @echo "üèóÔ∏è Running Terraform command in Nix environment:"
    @echo "üë®üèª‚Äçüíª Command: terraform {{CMDS}}"
    @echo "üìÇ Working directory: $(realpath {{WORKDIR}})"
    @cd "{{WORKDIR}}" && nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command terraform {{CMDS}}

# üåø Run Terraform commands locally with flexible module and command selection
tf-exec-mod MODULE='.' CMDS='--help':
    @echo "üèóÔ∏è Running Terraform command:"
    @echo "üë®üèª‚Äçüíª Command: terraform {{CMDS}}"
    @echo "üìÇ Working directory: $(realpath {{MODULES_DIR}}/{{MODULE}})"
    @cd "{{MODULES_DIR}}/{{MODULE}}" && terraform {{CMDS}}

# üåø Run Terraform commands in Nix development environment with flexible module and command selection
tf-exec-mod-cmd-nix MODULE='.' CMDS='--help':
    @echo "üèóÔ∏è Running Terraform command in Nix environment:"
    @echo "üë®üèª‚Äçüíª Command: terraform {{CMDS}}"
    @echo "üìÇ Working directory: $(realpath {{MODULES_DIR}}/{{MODULE}})"
    @cd "{{MODULES_DIR}}/{{MODULE}}" && nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command terraform {{CMDS}}

# üåø Run OpenTofu commands locally with flexible module and command selection
tofu-exec-mod MODULE='.' CMDS='--help':
    @echo "üèóÔ∏è Running OpenTofu command:"
    @echo "üë®üèª‚Äçüíª Command: tofu {{CMDS}}"
    @echo "üìÇ Working directory: $(realpath {{MODULES_DIR}}/{{MODULE}})"
    @cd "{{MODULES_DIR}}/{{MODULE}}" && tofu {{CMDS}}

# üåø Run OpenTofu commands in Nix development environment with flexible module and command selection
tofu-exec-mod-cmd-nix MODULE='.' CMDS='--help':
    @echo "üèóÔ∏è Running OpenTofu command in Nix environment:"
    @echo "üë®üèª‚Äçüíª Command: tofu {{CMDS}}"
    @echo "üìÇ Working directory: $(realpath {{MODULES_DIR}}/{{MODULE}})"
    @cd "{{MODULES_DIR}}/{{MODULE}}" && nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command tofu {{CMDS}}

# üîç Run TFLint on Terraform files to check for issues and best practices
tf-lint MODULE='':
    @echo "üîç Running TFLint on Terraform files..."
    @if [ -z "{{MODULE}}" ]; then \
        ./scripts/utilities/tflint.sh; \
    else \
        ./scripts/utilities/tflint.sh --module {{MODULE}}; \
    fi

# üîç Run TFLint on Terraform files using Nix environment
tf-lint-nix MODULE='':
    @echo "üîç Running TFLint on Terraform files in Nix environment..."
    @if [ -z "{{MODULE}}" ]; then \
        ./scripts/utilities/tflint.sh --nix; \
    else \
        ./scripts/utilities/tflint.sh --module {{MODULE}} --nix; \
    fi

# üìÑ Generate Terraform documentation using terraform-docs
tf-docs-generate MODULE='':
    @echo "üìÑ Generating Terraform documentation..."
    @if [ -z "{{MODULE}}" ]; then \
        ./scripts/utilities/tfdocs.sh; \
    else \
        ./scripts/utilities/tfdocs.sh --module {{MODULE}}; \
    fi

# üìÑ Generate Terraform documentation using terraform-docs in Nix environment
tf-docs-generate-nix MODULE='':
    @echo "üìÑ Generating Terraform documentation in Nix environment..."
    @if [ -z "{{MODULE}}" ]; then \
        ./scripts/utilities/tfdocs.sh --nix; \
    else \
        ./scripts/utilities/tfdocs.sh --module {{MODULE}} --nix; \
    fi

# üìÑ Validate Terraform modules locally using terraform validate
tf-validate MODULE='': (tf-exec-mod MODULE 'init -backend=false') (tf-exec-mod MODULE 'validate')

# üìÑ Validate Terraform modules in Nix development environment using terraform validate
tf-validate-nix MODULE='': (tf-exec-mod-cmd-nix MODULE 'init -backend=false') (tf-exec-mod-cmd-nix MODULE 'validate')

# üìÑ Run Terraform CI checks locally (only static, like 'fmt', 'lint', 'docs')
tf-ci-static MODULE='': (tf-format-check MODULE) (tf-lint MODULE) (tf-docs-generate MODULE) (tf-validate MODULE)

# üìÑ Run Terraform CI checks in Nix development environment
tf-ci-static-nix MODULE='': (tf-format-check-nix MODULE) (tf-lint-nix MODULE) (tf-docs-generate-nix MODULE) (tf-validate-nix MODULE)

# üåÄ Quick feedback loop for development E.g: just tf-dev "default" "basic" "true"
tf-dev MODULE='default' EXAMPLE='basic' FIXTURE='default.tfvars' CLEAN='false':
    @echo "üîÑ Cleaning up resources for module: {{MODULE}}, example: {{EXAMPLE}} (Clean: {{CLEAN}})"
    @if [ "{{CLEAN}}" = "true" ]; then \
        rm -rf "./modules/{{MODULE}}/.terraform" && \
        rm -rf "./examples/{{MODULE}}/{{EXAMPLE}}/.terraform" && \
        rm -f "./examples/{{MODULE}}/{{EXAMPLE}}/.terraform.lock.hcl"; \
        echo "‚úÖ Cleaned up resources for module: {{MODULE}}, example: {{EXAMPLE}}"; \
    else \
        echo "üõë No cleanup performed for module: {{MODULE}}, example: {{EXAMPLE}}"; \
    fi;

    @echo "üîç Running CI checks for module: {{MODULE}}"
    @just tf-ci-static "{{MODULE}}"

    @echo "üîç Initializing module: {{MODULE}}"
    @just tf-exec-mod "{{MODULE}}" 'init'

    @echo "üîç Initializing example: {{EXAMPLE}} for module: {{MODULE}}"
    @just tf-exec "examples/{{MODULE}}/{{EXAMPLE}}" 'init'

    @echo "üîç Validating example: {{EXAMPLE}} for module: {{MODULE}}"
    @just tf-exec "examples/{{MODULE}}/{{EXAMPLE}}" 'validate'

    @if [ -f "./examples/{{MODULE}}/{{EXAMPLE}}/{{FIXTURES_DIR}}/{{FIXTURE}}" ]; then \
        echo "üìÑ Using fixture: {{FIXTURES_DIR}}/{{FIXTURE}} for planning"; \
        just tf-exec "examples/{{MODULE}}/{{EXAMPLE}}" 'plan -var-file="{{FIXTURES_DIR}}/{{FIXTURE}}"'; \
    else \
        echo "üìÑ No fixture provided, running plan without it"; \
        just tf-exec "examples/{{MODULE}}/{{EXAMPLE}}" 'plan'; \
    fi

# üåÄ Quick feedback loop for development whic includes apply, and optionally destroy E.g: just tf-dev-apply "default" "basic" "default.tfvars" "true"
tf-dev-full MODULE='default' EXAMPLE='basic' FIXTURE='default.tfvars' CLEAN='false': (tf-dev MODULE EXAMPLE FIXTURE CLEAN)
    @echo "üöÄ Running apply for module: {{MODULE}}"
    @if [ -f "./examples/{{MODULE}}/{{EXAMPLE}}/{{FIXTURES_DIR}}/{{FIXTURE}}" ]; then \
        echo "üìÑ Using fixture: {{FIXTURES_DIR}}/{{FIXTURE}} for apply"; \
        just tf-exec "examples/{{MODULE}}/{{EXAMPLE}}" 'apply -var-file="{{FIXTURES_DIR}}/{{FIXTURE}}" -auto-approve'; \
    else \
        echo "üìÑ No fixture provided, running apply without it"; \
        just tf-exec "examples/{{MODULE}}/{{EXAMPLE}}" 'apply -auto-approve'; \
    fi

    @echo "üí£ Running destroy for module: {{MODULE}}"
    @if [ -f "./examples/{{MODULE}}/{{EXAMPLE}}/{{FIXTURES_DIR}}/{{FIXTURE}}" ]; then \
        echo "üìÑ Using fixture: {{FIXTURES_DIR}}/{{FIXTURE}} for destroy"; \
        just tf-exec "examples/{{MODULE}}/{{EXAMPLE}}" 'destroy -var-file="{{FIXTURES_DIR}}/{{FIXTURE}}" -auto-approve'; \
    else \
        echo "üìÑ No fixture provided, running destroy without it"; \
        just tf-exec "examples/{{MODULE}}/{{EXAMPLE}}" 'destroy -auto-approve'; \
    fi

# üåÄ Quick feedback loop for development in Nix environment which includes apply and destroy E.g: just tf-dev-full-nix "default" "basic" "default.tfvars" "true"
tf-dev-full-nix MODULE='default' EXAMPLE='basic' FIXTURE='default.tfvars' CLEAN='false': (tf-dev-full MODULE EXAMPLE FIXTURE CLEAN)
    @echo "üöÄ Running apply for module: {{MODULE}}"
    @if [ -f "./examples/{{MODULE}}/{{EXAMPLE}}/{{FIXTURES_DIR}}/{{FIXTURE}}" ]; then \
        echo "üìÑ Using fixture: {{FIXTURES_DIR}}/{{FIXTURE}} for apply"; \
        just tf-exec-nix "examples/{{MODULE}}/{{EXAMPLE}}" 'apply -var-file="{{FIXTURES_DIR}}/{{FIXTURE}}" -auto-approve'; \
    else \
        echo "üìÑ No fixture provided, running apply without it"; \
        just tf-exec-nix "examples/{{MODULE}}/{{EXAMPLE}}" 'apply -auto-approve'; \
    fi

    @echo "üí£ Running destroy for module: {{MODULE}}"
    @if [ -f "./examples/{{MODULE}}/{{EXAMPLE}}/{{FIXTURES_DIR}}/{{FIXTURE}}" ]; then \
        echo "üìÑ Using fixture: {{FIXTURES_DIR}}/{{FIXTURE}} for destroy"; \
        just tf-exec-nix "examples/{{MODULE}}/{{EXAMPLE}}" 'destroy -var-file="{{FIXTURES_DIR}}/{{FIXTURE}}" -auto-approve'; \
    else \
        echo "üìÑ No fixture provided, running destroy without it"; \
        just tf-exec-nix "examples/{{MODULE}}/{{EXAMPLE}}" 'destroy -auto-approve'; \
    fi


# üåÄ Quick feedback loop for development in Nix environment
tf-dev-nix MODULE='default' EXAMPLE='basic' FIXTURE='default.tfvars' CLEAN='false':
    @echo "üîÑ Cleaning up resources for module: {{MODULE}}, example: {{EXAMPLE}} (Clean: {{CLEAN}})"
    @if [ "{{CLEAN}}" = "true" ]; then \
        rm -rf "./modules/{{MODULE}}/.terraform" && \
        rm -rf "./examples/{{MODULE}}/{{EXAMPLE}}/.terraform" && \
        rm -f "./examples/{{MODULE}}/{{EXAMPLE}}/.terraform.lock.hcl"; \
        echo "‚úÖ Cleaned up resources for module: {{MODULE}}, example: {{EXAMPLE}}"; \
    else \
        echo "üõë No cleanup performed for module: {{MODULE}}, example: {{EXAMPLE}}"; \
    fi;

    @echo "üîç Running CI checks for module: {{MODULE}}"
    @just tf-ci-static-nix "{{MODULE}}"

    @echo "üîç Initializing module: {{MODULE}}"
    @just tf-cmd-nix "{{MODULE}}" 'init'

    @echo "üîç Initializing example: {{EXAMPLE}} for module: {{MODULE}}"
    @just tf-exec-nix "examples/{{MODULE}}/{{EXAMPLE}}" 'init'

    @echo "üîç Validating example: {{EXAMPLE}} for module: {{MODULE}}"
    @just tf-exec-nix "examples/{{MODULE}}/{{EXAMPLE}}" 'validate'

    @if [ -f "./examples/{{MODULE}}/{{EXAMPLE}}/{{FIXTURES_DIR}}/{{FIXTURE}}" ]; then \
        echo "üìÑ Using fixture: {{FIXTURES_DIR}}/{{FIXTURE}} for planning"; \
        just tf-exec-nix "examples/{{MODULE}}/{{EXAMPLE}}" 'plan -var-file="{{FIXTURES_DIR}}/{{FIXTURE}}"'; \
    else \
        echo "üìÑ No fixture provided, running plan without it"; \
        just tf-exec-nix "examples/{{MODULE}}/{{EXAMPLE}}" 'plan'; \
    fi

# üß™ Run unit tests - parameters: TAGS (E.g. 'readonly' or 'integration'), MOD (module name), NOCACHE (true/false), TIMEOUT (E.g. '60s|5m|1h')
tf-test-unit TAGS='readonly' MODULE='default' NOCACHE='true' TIMEOUT='60s':
    @echo "üß™ Running unit tests..."
    @echo "üìã Configuration:"
    @echo "   üè∑Ô∏è  Tags: unit,{{TAGS}}"
    @echo "   üîç Module: {{MODULE}}"
    @echo "   üîÑ No Cache: {{NOCACHE}}"
    @echo "   ‚è±Ô∏è  Timeout: {{TIMEOUT}}"

    @if ! echo "{{TIMEOUT}}" | grep -qE '^[0-9]+[smh]$'; then \
        echo "‚ùå Invalid timeout format. Use format like '60s', '5m', or '1h'"; \
        exit 1; \
    fi

    @cd {{TESTS_DIR}} && \
    if [ -z "{{MODULE}}" ] || [ "{{MODULE}}" = "default" ]; then \
        echo "üîç Running unit tests for module: default in path {{TESTS_DIR}}/modules/{{MODULE}}/unit" && \
        echo "üßπ Cleaning up terraform state" && \
        find . -type d -name ".terraform" -exec rm -rf {} \; 2>/dev/null || true; \
        find . -type f -name ".terraform.lock.hcl" -delete 2>/dev/null || true; \
        go test \
            -v \
            -tags "unit,{{TAGS}}" \
            $(if [ "{{NOCACHE}}" = "true" ]; then echo "-count=1"; fi) \
            -timeout="{{TIMEOUT}}" \
            ./modules/default/unit/...; \
    else \
        if [ -d "./modules/{{MODULE}}/unit" ]; then \
            echo "üîç Running unit tests for module: {{MODULE}}" && \
            echo "üßπ Cleaning up terraform state" && \
            find "./modules/{{MODULE}}/unit" -type d -name ".terraform" -exec rm -rf {} \; 2>/dev/null || true; \
            find "./modules/{{MODULE}}/unit" -type f -name ".terraform.lock.hcl" -delete 2>/dev/null || true; \
            go test \
                -v \
                -tags "unit,{{TAGS}}" \
                $(if [ "{{NOCACHE}}" = "true" ]; then echo "-count=1"; fi) \
                -timeout="{{TIMEOUT}}" \
                "./modules/{{MODULE}}/unit/..."; \
        else \
            echo "‚ùå Unit test directory not found: ./modules/{{MODULE}}/unit"; \
            exit 1; \
        fi; \
    fi

# üß™ Run unit tests in Nix environment - parameters: TAGS (E.g. 'readonly' or 'integration'), MOD (module name), NOCACHE (true/false), TIMEOUT (E.g. '60s|5m|1h')
tf-test-unit-nix TAGS='readonly' MODULE='default' NOCACHE='true' TIMEOUT='60s':
    @echo "üß™ Running unit tests in Nix environment..."
    @echo "üìã Configuration:"
    @echo "   üè∑Ô∏è  Tags: unit,{{TAGS}}"
    @echo "   üîç Module: {{MODULE}}"
    @echo "   üîÑ No Cache: {{NOCACHE}}"
    @echo "   ‚è±Ô∏è  Timeout: {{TIMEOUT}}"

    @if ! echo "{{TIMEOUT}}" | grep -qE '^[0-9]+[smh]$'; then \
        echo "‚ùå Invalid timeout format. Use format like '60s', '5m', or '1h'"; \
        exit 1; \
    fi

    @nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command bash -c "cd {{TESTS_DIR}} && \
    if [ -z '{{MODULE}}' ] || [ '{{MODULE}}' = 'default' ]; then \
        echo 'üîç Running unit tests for module: default' && \
        find . -type d -name '.terraform' -exec rm -rf {} \; 2>/dev/null || true; \
        find . -type f -name '.terraform.lock.hcl' -delete 2>/dev/null || true; \
        go test \
            -v \
            -tags 'unit,{{TAGS}}' \
            $(if [ '{{NOCACHE}}' = 'true' ]; then echo '-count=1'; fi) \
            -timeout='{{TIMEOUT}}' \
            ./modules/default/unit/...; \
    else \
        if [ -d './modules/{{MODULE}}/unit' ]; then \
            echo 'üîç Running unit tests for module: {{MODULE}}' && \
            find './modules/{{MODULE}}/unit' -type d -name '.terraform' -exec rm -rf {} \; 2>/dev/null || true; \
            find './modules/{{MODULE}}/unit' -type f -name '.terraform.lock.hcl' -delete 2>/dev/null || true; \
            go test \
                -v \
                -tags 'unit,{{TAGS}}' \
                $(if [ '{{NOCACHE}}' = 'true' ]; then echo '-count=1'; fi) \
                -timeout='{{TIMEOUT}}' \
                './modules/{{MODULE}}/unit/...'; \
        else \
            echo '‚ùå Unit test directory not found: ./modules/{{MODULE}}/unit'; \
            exit 1; \
        fi; \
    fi"

# üß™ Run examples tests - parameters: TAGS (E.g. 'readonly' or 'integration'), MOD (module name), NOCACHE (true/false), TIMEOUT (E.g. '60s|5m|1h')
tf-test-examples TAGS='readonly' MODULE='default' NOCACHE='true' TIMEOUT='60s':
    @echo "üß™ Running example tests..."
    @echo "üìã Configuration:"
    @echo "   üè∑Ô∏è  Tags: examples,{{TAGS}}"
    @echo "   üîç Module: {{MODULE}}"
    @echo "   üîÑ No Cache: {{NOCACHE}}"
    @echo "   ‚è±Ô∏è  Timeout: {{TIMEOUT}}"

    @if ! echo "{{TIMEOUT}}" | grep -qE '^[0-9]+[smh]$'; then \
        echo "‚ùå Invalid timeout format. Use format like '60s', '5m', or '1h'"; \
        exit 1; \
    fi

    @cd {{TESTS_DIR}} && \
    if [ -z "{{MODULE}}" ] || [ "{{MODULE}}" = "default" ]; then \
        echo "üîç Running examples tests for module: default in path {{TESTS_DIR}}/modules/{{MODULE}}/examples" && \
        echo "üßπ Cleaning up terraform state" && \
        find . -type d -name ".terraform" -exec rm -rf {} \; 2>/dev/null || true; \
        find . -type f -name ".terraform.lock.hcl" -delete 2>/dev/null || true; \
        go test \
            -v \
            -tags "examples,{{TAGS}}" \
            $(if [ "{{NOCACHE}}" = "true" ]; then echo "-count=1"; fi) \
            -timeout="{{TIMEOUT}}" \
            ./modules/default/examples/...; \
    else \
        if [ -d "./modules/{{MODULE}}/examples" ]; then \
            echo "üîç Running examples tests for module: {{MODULE}}" && \
            echo "üßπ Cleaning up terraform state" && \
            find "./modules/{{MODULE}}/examples" -type d -name ".terraform" -exec rm -rf {} \; 2>/dev/null || true; \
            find "./modules/{{MODULE}}/examples" -type f -name ".terraform.lock.hcl" -delete 2>/dev/null || true; \
            go test \
                -v \
                -tags "examples,{{TAGS}}" \
                $(if [ "{{NOCACHE}}" = "true" ]; then echo "-count=1"; fi) \
                -timeout="{{TIMEOUT}}" \
                "./modules/{{MODULE}}/examples/..."; \
        else \
            echo "‚ùå Examples test directory not found: ./modules/{{MODULE}}/examples"; \
            exit 1; \
        fi; \
    fi

# üß™ Run examples tests in Nix environment - parameters: TAGS (E.g. 'readonly' or 'integration'), MOD (module name), NOCACHE (true/false), TIMEOUT (E.g. '60s|5m|1h')
tf-test-examples-nix TAGS='readonly' MODULE='default' NOCACHE='true' TIMEOUT='60s':
    @echo "üß™ Running example tests in Nix environment..."
    @echo "üìã Configuration:"
    @echo "   üè∑Ô∏è  Tags: examples,{{TAGS}}"
    @echo "   üîç Module: {{MODULE}}"
    @echo "   üîÑ No Cache: {{NOCACHE}}"
    @echo "   ‚è±Ô∏è  Timeout: {{TIMEOUT}}"

    @if ! echo "{{TIMEOUT}}" | grep -qE '^[0-9]+[smh]$'; then \
        echo "‚ùå Invalid timeout format. Use format like '60s', '5m', or '1h'"; \
        exit 1; \
    fi

    @nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command bash -c "cd {{TESTS_DIR}} && \
    if [ -z '{{MODULE}}' ] || [ '{{MODULE}}' = 'default' ]; then \
        echo 'üîç Running examples tests for module: default' && \
        find . -type d -name '.terraform' -exec rm -rf {} \; 2>/dev/null || true; \
        find . -type f -name '.terraform.lock.hcl' -delete 2>/dev/null || true; \
        go test \
            -v \
            -tags 'examples,{{TAGS}}' \
            $(if [ '{{NOCACHE}}' = 'true' ]; then echo '-count=1'; fi) \
            -timeout='{{TIMEOUT}}' \
            ./modules/default/examples/...; \
    else \
        if [ -d './modules/{{MODULE}}/examples' ]; then \
            echo 'üîç Running examples tests for module: {{MODULE}}' && \
            find './modules/{{MODULE}}/examples' -type d -name '.terraform' -exec rm -rf {} \; 2>/dev/null || true; \
            find './modules/{{MODULE}}/examples' -type f -name '.terraform.lock.hcl' -delete 2>/dev/null || true; \
            go test \
                -v \
                -tags 'examples,{{TAGS}}' \
                $(if [ '{{NOCACHE}}' = 'true' ]; then echo '-count=1'; fi) \
                -timeout='{{TIMEOUT}}' \
                './modules/{{MODULE}}/examples/...'; \
        else \
            echo '‚ùå Examples test directory not found: ./modules/{{MODULE}}/examples'; \
            exit 1; \
        fi; \
    fi"

# üî® Build the Dagger pipeline
[working-directory:'pipeline/infra']
pipeline-infra-build:
    @echo "üî® Initializing Dagger development environment"
    @dagger develop
    @echo "üìã Building Dagger pipeline"
    @dagger functions

# üî® Help for Dagger job
[working-directory:'pipeline/infra']
pipeline-job-help fn: (pipeline-infra-build)
    @echo "üî® Help for Dagger job: {{fn}}"
    @dagger call {{fn}} --help

# üî® Open an interactive development shell for the Infra pipeline
[working-directory:'pipeline/infra']
pipeline-infra-shell args="": (pipeline-infra-build)
    @echo "üöÄ Launching interactive terminal"
    @dagger call open-terminal {{args}}

# üî® Execute a Dagger job with a specified command and arguments
[working-directory:'pipeline/infra']
pipeline-job-exec mod="default" command="init" args="": (pipeline-infra-build)
    @echo "üöÄ Executing job: {{command}} with arguments: {{args}}"
    @dagger --use-hashicorp-image=true call job-terraform-exec \
       --command="{{command}}" \
       --tf-module-path="{{mod}}" \
       --arguments="{{args}}" \
       --load-dot-env-file=true \
       --no-cache=true \
       --tflint-version="0.58.0" \
       --terraform-docs-version="0.20.0" \
       --git-ssh $SSH_AUTH_SOCK

# üî® Perform static analysis on Terraform modules for security and best practices
[working-directory:'pipeline/infra']
pipeline-action-terraform-static-analysis MODULE="default" args="": (pipeline-infra-build)
    @echo " Analyzing Terraform modules for security and best practices"
    @echo "‚ö° Running static analysis checks"
    @dagger --use-hashicorp-image=true call action-terraform-static-analysis \
       --tf-module-path="{{MODULE}}" \
       --load-dot-env-file=true \
       --no-cache=true \
       --git-ssh $SSH_AUTH_SOCK
    @echo "‚úÖ Static analysis completed successfully"

# üî® Verify compatibility of Terraform modules across different provider versions
[working-directory:'pipeline/infra']
pipeline-action-terraform-version-compatibility-verification MODULE="default": (pipeline-infra-build)
    @echo " Testing module compatibility across provider versions"
    @echo "‚ö° Running version compatibility checks"
    @dagger --use-hashicorp-image=true call action-terraform-version-compatibility-verification \
       --tf-module-path="{{MODULE}}" \
       --load-dot-env-file=true \
       --no-cache=true \
       --git-ssh $SSH_AUTH_SOCK
    @echo "‚úÖ Version compatibility testing completed"

# üî® Verify the integrity of Terraform module files
[working-directory:'pipeline/infra']
pipeline-action-terraform-file-verification MODULE="default": (pipeline-infra-build)
    @echo " Testing module files"
    @echo "‚ö° Running file verification"
    @dagger --use-hashicorp-image=true call action-terraform-file-verification \
       --tf-module-path="{{MODULE}}" \
       --load-dot-env-file=true \
       --no-cache=true
    @echo "‚úÖ File verification completed"

# üî® Build Terraform modules
[working-directory:'pipeline/infra']
pipeline-action-terraform-build MODULE="default": (pipeline-infra-build)
    @echo " Building module files"
    @echo "‚ö° Running plan"
    @dagger --use-hashicorp-image=true call action-terraform-build \
       --tf-module-path="{{MODULE}}" \
       --load-dot-env-file=true \
       --no-cache=true
    @echo "‚úÖ Build completed"

# üî® Generate module documentation
[working-directory:'pipeline/infra']
pipeline-action-terraform-docs MODULE="default": (pipeline-infra-build)
    @echo " Generating module documentation"
    @echo "‚ö° Running docs"
    @dagger --use-hashicorp-image=true call action-terraform-docs \
       --tf-module-path="{{MODULE}}" \
       --load-dot-env-file=true \
       --no-cache=true
    @echo "‚úÖ Docs completed"

[working-directory:'pipeline/infra']
pipeline-action-terraform-lint MODULE="default": (pipeline-infra-build)
    @echo " Linting module files"
    @echo "‚ö° Running lint"
    @dagger --use-hashicorp-image=true call action-terraform-lint \
       --tf-module-path="{{MODULE}}" \
       --load-dot-env-file=true \
       --no-cache=true
    @echo "‚úÖ Lint completed"

# üî® Run comprehensive CI checks for Terraform modules
pipeline-infra-tf-ci MODULE="default" args="": (pipeline-action-terraform-static-analysis MODULE args) (pipeline-action-terraform-version-compatibility-verification MODULE) (pipeline-action-terraform-file-verification MODULE)
