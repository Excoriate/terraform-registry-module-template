# ğŸš€ Terraform Module Development Workflow: Automate setup, formatting, linting, and project management

# ğŸš Shell configuration
# Use bash with strict error handling to prevent silent failures
# -u: Treat unset variables as an error
# -e: Exit immediately if a command exits with a non-zero status
set shell := ["bash", "-uce"]
set dotenv-load

# Avoid reporting traces to Dagger
# TODO: Uncomment if traces aren't needed.
# export NOTHANKS := "1"

# ğŸ¯ Default task: Display available recipes when no specific task is specified
default: help

# ğŸ“¦ Variables for project directories
TESTS_DIR := 'tests/modules'
MODULES_DIR := 'modules'
EXAMPLES_DIR := 'examples'
FIXTURES_DIR := 'fixtures'

# â„¹ï¸ List all available recipes with their descriptions
help:
    @just --list

# ğŸ”§ Install pre-commit hooks in Nix environment for consistent code quality
hooks-install-nix:
    @echo "ğŸ§° Installing pre-commit hooks in Nix environment..."
    @nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command pre-commit install

# ğŸ”§ Install pre-commit hooks in local environment for code consistency
hooks-install:
    @echo "ğŸ§° Installing pre-commit hooks locally..."
    @./scripts/hooks/pre-commit-init.sh init

# ğŸ•µï¸ Run pre-commit hooks across all files in Nix environment
hooks-run-nix:
    @echo "ğŸ” Running pre-commit hooks from .pre-commit-config.yaml in Nix environment..."
    @nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command pre-commit run --all-files

# ğŸ•µï¸ Run pre-commit hooks across all files in local environment
hooks-run:
    @echo "ğŸ” Running pre-commit hooks from .pre-commit-config.yaml..."
    @./scripts/hooks/pre-commit-init.sh run

# ğŸ” Check if a module is a Terraform module
is-tf-module MOD='default':
    @echo "ğŸ” Checking if module: {{MODULES_DIR}}/{{MOD}} is a Terraform module..."
    @if [ -z "$(find "{{MODULES_DIR}}/{{MOD}}" -type f -name '*.tf')" ]; then \
        echo "âŒ No Terraform files found in module: {{MODULES_DIR}}/{{MOD}}"; \
        exit 1; \
    fi

# ğŸ§¹ Remove Terraform and Terragrunt cache directories to reset project state
clean-tf:
    @echo "ğŸ—‘ï¸ Cleaning Terraform and Terragrunt cache directories across the entire repository..."
    find . -type d -name ".terraform" -exec rm -rf {} +; \
    find . -type d -name ".terragrunt-cache" -exec rm -rf {} +; \
    find . -type f -name "*.tfstate" -exec rm -f {} +; \
    find . -type f -name "*.tfstate.backup" -exec rm -f {} +; \
    echo "âœ… Cleanup complete!"

# ğŸ§¹ Remove Terraform and Terragrunt cache directories for a specific module
clean-tf-mod MOD='default': (is-tf-module MOD)
    @echo "ğŸ—‘ï¸ Cleaning Terraform and Terragrunt cache directories for module: {{MOD}}..."
    @echo "ğŸ” Found module: {{MODULES_DIR}}/{{MOD}}"
    @echo "ğŸ“‚ Listing directories and files in module: {{MODULES_DIR}}/{{MOD}}"
    @ls -R "{{MODULES_DIR}}/{{MOD}}"
    find "{{MODULES_DIR}}/{{MOD}}" -type d -name ".terraform" -exec rm -rf {} +; \
    find "{{MODULES_DIR}}/{{MOD}}" -type d -name ".terragrunt-cache" -exec rm -rf {} +; \
    find "{{MODULES_DIR}}/{{MOD}}" -type f -name "*.tfstate" -exec rm -f {} +; \
    find "{{MODULES_DIR}}/{{MOD}}" -type f -name "*.tfstate.backup" -exec rm -f {} +; \
    echo "âœ… Cleanup complete!"

# ğŸ§¹ Comprehensive cleanup of project artifacts, state files, and cache directories
clean:
    @echo "ğŸ—‘ï¸ Performing comprehensive project cleanup for general purposes..."
    @find . -name ".DS_Store" -exec rm -f {} +
    @find . -name "*.log" -exec rm -f {} +

# ğŸ§¹ Comprehensive cleanup of project artifacts, state files, and cache directories in Nix environment
clean-all: clean clean-tf

# ğŸ§ Format YAML files using yamlfmt in Nix environment
yaml-fix-nix:
    @echo "ğŸ”§ Formatting YAML files with yamlfmt in Nix environment..."
    @nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command yamlfmt .

# ğŸ§¹ Format and lint YAML files for consistency and quality
yaml-fix:
    @echo "ğŸ”§ Formatting and linting YAML files..."
    @yamlfmt .
    @echo "ğŸ•µï¸ Validating YAML configuration..."
    @yamllint --config-file .yamllint.yml --strict .
    @echo "âœ… YAML formatting and linting complete!"

# ğŸ•µï¸ Lint YAML files using yamllint in Nix environment
yaml-lint-nix:
    @echo "ğŸ•µï¸ Linting YAML files in Nix environment..."
    @nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command yamllint .

# ğŸ•µï¸ Validate YAML files against strict configuration standards
yaml-lint:
    @echo "ğŸ•µï¸ Linting YAML files..."
    @yamlfmt .
    @echo "ğŸ•µï¸ Checking yamllint configuration..."
    @yamllint --config-file .yamllint.yml --strict .
    @echo "âœ… YAML formatting and linting complete!"

# ğŸš Lint shell scripts using shellcheck in Nix environment
scripts-lint-nix:
    @echo "ğŸš Linting shell scripts in Nix environment..."
    @nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command bash -c 'find . -type f -name "*.sh" | xargs shellcheck'

# ğŸš Perform static analysis on all shell scripts
scripts-lint:
    @echo "ğŸš Linting shell scripts..."
    @find . -type f -name "*.sh" | xargs shellcheck

# ğŸ¦« Lint Go files using custom script in Nix environment
go-lint-nix:
    @echo "ğŸ¦« Linting Go files in Nix environment..."
    @nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command bash -c 'cd tests/ && go mod tidy && golangci-lint run --verbose --config ../.golangci.yml'

# ğŸ¦« Perform static code analysis on Go files
go-lint:
    @echo "ğŸ¦« Linting Go files..."
    @cd tests/ && go mod tidy && golangci-lint run --verbose --config ../.golangci.yml

# ğŸ¹ Format Go files in Nix environment using gofmt
go-format-nix:
    @echo "ğŸ¹ Formatting Go files in Nix environment..."
    @nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command bash -c 'find . -type f -name "*.go" -not -path "*/vendor/*" -exec gofmt -w {} +'

# ğŸ¹ Format Go files locally within the tests directory
go-format:
    @echo "ğŸ¹ Formatting Go files in tests directory..."
    @cd tests && \
    echo "ğŸ“‹ Go files to be formatted:" && \
    find . -type f -name "*.go" -not -path "*/vendor/*" | tee /dev/tty | xargs gofmt -w

# ğŸ¹ Tidy Go files in Nix environment
go-tidy-nix:
    @echo "ğŸ¹ Tidying Go files in Nix environment..."
    @nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command bash -c 'cd tests/ && go mod tidy'

# ğŸ¹ Tidy Go files locally within the tests directory
go-tidy:
    @echo "ğŸ¹ Tidying Go files in tests directory..."
    @cd tests && go mod tidy

# ğŸ¹ Comprehensive CI checks for Go files
go-ci: (go-tidy) (go-format) (go-lint)
    @echo "âœ… Go files CI checks completed"

# ğŸ¹ Comprehensive CI checks for Go files in Nix environment
go-ci-nix: (go-tidy-nix) (go-format-nix) (go-lint-nix)
    @echo "âœ… Go files CI checks completed in Nix environment"

# ğŸš€ Launch Nix development shell with project dependencies
dev:
    @echo "ğŸŒ¿ Starting Nix Development Shell for Terraform Registry Module Template ğŸ·ï¸"
    @nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes

# ğŸ”“ Enable direnv for environment variable management
allow-direnv:
    @echo "ğŸ”“ Allowing direnv in the current directory..."
    @direnv allow

# ğŸ”„ Reload direnv environment configuration
reload-direnv:
    @echo "ğŸ” Reloading direnv environment..."
    @direnv reload

# ğŸŒ¿ Format Terraform files locally using terraform fmt
tf-format:
    @echo "ğŸ¨ Formatting Terraform files in current directory..."
    @./scripts/utilities/format.sh --terraform

# ğŸŒ¿ Format all Terraform files across modules, examples, and tests directories
tf-format-all:
    @echo "ğŸ¨ Formatting all Terraform files across repository..."
    @./scripts/utilities/format.sh --terraform --tf-all-dirs

# ğŸŒ¿ Format Terraform files for a specific module (both module and example directories)
tf-format-module MODULE:
    @echo "ğŸ¨ Formatting Terraform files for module: {{MODULE}}"
    @./scripts/utilities/format.sh --terraform --tf-module {{MODULE}}

# ğŸ” Check Terraform file formatting without modifying files
tf-format-check MOD='':
    @echo "ğŸ” Checking Terraform file formatting..."
    @if [ -z "{{MOD}}" ]; then \
        ./scripts/utilities/format.sh --terraform --tf-check; \
    else \
        ./scripts/utilities/format.sh --terraform --tf-check --tf-module {{MOD}}; \
    fi

# ğŸ” Check Terraform file formatting for all directories
tf-format-check-all:
    @echo "ğŸ” Checking Terraform file formatting across all directories..."
    @./scripts/utilities/format.sh --terraform --tf-check --tf-all-dirs

# ğŸ” Check Terraform file formatting for a specific module
tf-format-check-module MODULE:
    @echo "ğŸ” Checking Terraform file formatting for module: {{MODULE}}"
    @./scripts/utilities/format.sh --terraform --tf-check --tf-module {{MODULE}}

# ğŸ” Check Terraform file formatting without modifying files (Nix environment)
tf-format-check-nix MOD='':
    @echo "ğŸ” Checking Terraform file formatting in Nix environment..."
    @if [ -z "{{MOD}}" ]; then \
        nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command ./scripts/utilities/format.sh --terraform --tf-check; \
    else \
        nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command ./scripts/utilities/format.sh --terraform --tf-check --tf-module {{MOD}}; \
    fi

# ğŸ” Discover and list all Terraform files in the repository
tf-discover:
    @echo "ğŸ” Discovering Terraform files..."
    @./scripts/utilities/format.sh --terraform --tf-discover

# ğŸŒ¿ Run Terraform commands with flexible working directory and command selection
tf-exec WORKDIR='.' CMDS='--help':
    @echo "ğŸ—ï¸ Running Terraform command:"
    @echo "ğŸ‘¨ğŸ»â€ğŸ’» Command: terraform {{CMDS}}"
    @echo "ğŸ“‚ Working directory: $(realpath {{WORKDIR}})"
    @cd "{{WORKDIR}}" && terraform {{CMDS}}

# ğŸŒ¿ Run Terraform commands in Nix development environment with flexible working directory and command selection
tf-exec-nix WORKDIR='.' CMDS='--help':
    @echo "ğŸ—ï¸ Running Terraform command in Nix environment:"
    @echo "ğŸ‘¨ğŸ»â€ğŸ’» Command: terraform {{CMDS}}"
    @echo "ğŸ“‚ Working directory: $(realpath {{WORKDIR}})"
    @cd "{{WORKDIR}}" && nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command terraform {{CMDS}}

# ğŸŒ¿ Run Terraform commands locally with flexible module and command selection
tf-exec-mod MODULE='.' CMDS='--help':
    @echo "ğŸ—ï¸ Running Terraform command:"
    @echo "ğŸ‘¨ğŸ»â€ğŸ’» Command: terraform {{CMDS}}"
    @echo "ğŸ“‚ Working directory: $(realpath {{MODULES_DIR}}/{{MODULE}})"
    @cd "{{MODULES_DIR}}/{{MODULE}}" && terraform {{CMDS}}

# ğŸŒ¿ Run Terraform commands in Nix development environment with flexible module and command selection
tf-exec-mod-cmd-nix MODULE='.' CMDS='--help':
    @echo "ğŸ—ï¸ Running Terraform command in Nix environment:"
    @echo "ğŸ‘¨ğŸ»â€ğŸ’» Command: terraform {{CMDS}}"
    @echo "ğŸ“‚ Working directory: $(realpath {{MODULES_DIR}}/{{MODULE}})"
    @cd "{{MODULES_DIR}}/{{MODULE}}" && nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command terraform {{CMDS}}

# ğŸŒ¿ Run OpenTofu commands locally with flexible module and command selection
tofu-exec-mod MODULE='.' CMDS='--help':
    @echo "ğŸ—ï¸ Running OpenTofu command:"
    @echo "ğŸ‘¨ğŸ»â€ğŸ’» Command: tofu {{CMDS}}"
    @echo "ğŸ“‚ Working directory: $(realpath {{MODULES_DIR}}/{{MODULE}})"
    @cd "{{MODULES_DIR}}/{{MODULE}}" && tofu {{CMDS}}

# ğŸŒ¿ Run OpenTofu commands in Nix development environment with flexible module and command selection
tofu-exec-mod-cmd-nix MODULE='.' CMDS='--help':
    @echo "ğŸ—ï¸ Running OpenTofu command in Nix environment:"
    @echo "ğŸ‘¨ğŸ»â€ğŸ’» Command: tofu {{CMDS}}"
    @echo "ğŸ“‚ Working directory: $(realpath {{MODULES_DIR}}/{{MODULE}})"
    @cd "{{MODULES_DIR}}/{{MODULE}}" && nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command tofu {{CMDS}}

# ğŸ” Run TFLint on Terraform files to check for issues and best practices
tf-lint MODULE='':
    @echo "ğŸ” Running TFLint on Terraform files..."
    @if [ -z "{{MODULE}}" ]; then \
        ./scripts/utilities/tflint.sh; \
    else \
        ./scripts/utilities/tflint.sh --module {{MODULE}}; \
    fi

# ğŸ” Run TFLint on Terraform files using Nix environment
tf-lint-nix MODULE='':
    @echo "ğŸ” Running TFLint on Terraform files in Nix environment..."
    @if [ -z "{{MODULE}}" ]; then \
        ./scripts/utilities/tflint.sh --nix; \
    else \
        ./scripts/utilities/tflint.sh --module {{MODULE}} --nix; \
    fi

# ğŸ“„ Generate Terraform documentation using terraform-docs
tf-docs-generate MODULE='':
    @echo "ğŸ“„ Generating Terraform documentation..."
    @if [ -z "{{MODULE}}" ]; then \
        ./scripts/utilities/tfdocs.sh; \
    else \
        ./scripts/utilities/tfdocs.sh --module {{MODULE}}; \
    fi

# ğŸ“„ Generate Terraform documentation using terraform-docs in Nix environment
tf-docs-generate-nix MODULE='':
    @echo "ğŸ“„ Generating Terraform documentation in Nix environment..."
    @if [ -z "{{MODULE}}" ]; then \
        ./scripts/utilities/tfdocs.sh --nix; \
    else \
        ./scripts/utilities/tfdocs.sh --module {{MODULE}} --nix; \
    fi

# ğŸ“„ Validate Terraform modules locally using terraform validate
tf-validate MODULE='': (tf-exec-mod MODULE 'init -backend=false') (tf-exec-mod MODULE 'validate')

# ğŸ“„ Validate Terraform modules in Nix development environment using terraform validate
tf-validate-nix MODULE='': (tf-exec-mod-cmd-nix MODULE 'init -backend=false') (tf-exec-mod-cmd-nix MODULE 'validate')

# ğŸ“„ Run Terraform CI checks locally (only static, like 'fmt', 'lint', 'docs')
tf-ci-static MODULE='': (tf-format-check MODULE) (tf-lint MODULE) (tf-docs-generate MODULE) (tf-validate MODULE)

# ğŸ“„ Run Terraform CI checks in Nix development environment
tf-ci-static-nix MODULE='': (tf-format-check-nix MODULE) (tf-lint-nix MODULE) (tf-docs-generate-nix MODULE) (tf-validate-nix MODULE)

# ğŸŒ€ Quick feedback loop for development E.g: just tf-dev "default" "basic" "true"
tf-dev MODULE='default' EXAMPLE='basic' FIXTURE='default.tfvars' CLEAN='false':
    @echo "ğŸ”„ Cleaning up resources for module: {{MODULE}}, example: {{EXAMPLE}} (Clean: {{CLEAN}})"
    @if [ "{{CLEAN}}" = "true" ]; then \
        rm -rf "./modules/{{MODULE}}/.terraform" && \
        rm -rf "./examples/{{MODULE}}/{{EXAMPLE}}/.terraform" && \
        rm -f "./examples/{{MODULE}}/{{EXAMPLE}}/.terraform.lock.hcl"; \
        echo "âœ… Cleaned up resources for module: {{MODULE}}, example: {{EXAMPLE}}"; \
    else \
        echo "ğŸ›‘ No cleanup performed for module: {{MODULE}}, example: {{EXAMPLE}}"; \
    fi;

    @echo "ğŸ” Running CI checks for module: {{MODULE}}"
    @just tf-ci-static "{{MODULE}}"

    @echo "ğŸ” Initializing module: {{MODULE}}"
    @just tf-exec-mod "{{MODULE}}" 'init'

    @echo "ğŸ” Initializing example: {{EXAMPLE}} for module: {{MODULE}}"
    @just tf-exec "examples/{{MODULE}}/{{EXAMPLE}}" 'init'

    @echo "ğŸ” Validating example: {{EXAMPLE}} for module: {{MODULE}}"
    @just tf-exec "examples/{{MODULE}}/{{EXAMPLE}}" 'validate'

    @if [ -f "./examples/{{MODULE}}/{{EXAMPLE}}/{{FIXTURES_DIR}}/{{FIXTURE}}" ]; then \
        echo "ğŸ“„ Using fixture: {{FIXTURES_DIR}}/{{FIXTURE}} for planning"; \
        just tf-exec "examples/{{MODULE}}/{{EXAMPLE}}" 'plan -var-file="{{FIXTURES_DIR}}/{{FIXTURE}}"'; \
    else \
        echo "ğŸ“„ No fixture provided, running plan without it"; \
        just tf-exec "examples/{{MODULE}}/{{EXAMPLE}}" 'plan'; \
    fi

# ğŸŒ€ Quick feedback loop for development whic includes apply, and optionally destroy E.g: just tf-dev-apply "default" "basic" "default.tfvars" "true"
tf-dev-full MODULE='default' EXAMPLE='basic' FIXTURE='default.tfvars' CLEAN='false': (tf-dev MODULE EXAMPLE FIXTURE CLEAN)
    @echo "ğŸš€ Running apply for module: {{MODULE}}"
    @if [ -f "./examples/{{MODULE}}/{{EXAMPLE}}/{{FIXTURES_DIR}}/{{FIXTURE}}" ]; then \
        echo "ğŸ“„ Using fixture: {{FIXTURES_DIR}}/{{FIXTURE}} for apply"; \
        just tf-exec "examples/{{MODULE}}/{{EXAMPLE}}" 'apply -var-file="{{FIXTURES_DIR}}/{{FIXTURE}}" -auto-approve'; \
    else \
        echo "ğŸ“„ No fixture provided, running apply without it"; \
        just tf-exec "examples/{{MODULE}}/{{EXAMPLE}}" 'apply -auto-approve'; \
    fi

    @echo "ğŸ’£ Running destroy for module: {{MODULE}}"
    @if [ -f "./examples/{{MODULE}}/{{EXAMPLE}}/{{FIXTURES_DIR}}/{{FIXTURE}}" ]; then \
        echo "ğŸ“„ Using fixture: {{FIXTURES_DIR}}/{{FIXTURE}} for destroy"; \
        just tf-exec "examples/{{MODULE}}/{{EXAMPLE}}" 'destroy -var-file="{{FIXTURES_DIR}}/{{FIXTURE}}" -auto-approve'; \
    else \
        echo "ğŸ“„ No fixture provided, running destroy without it"; \
        just tf-exec "examples/{{MODULE}}/{{EXAMPLE}}" 'destroy -auto-approve'; \
    fi

# ğŸŒ€ Quick feedback loop for development in Nix environment which includes apply and destroy E.g: just tf-dev-full-nix "default" "basic" "default.tfvars" "true"
tf-dev-full-nix MODULE='default' EXAMPLE='basic' FIXTURE='default.tfvars' CLEAN='false': (tf-dev-full MODULE EXAMPLE FIXTURE CLEAN)
    @echo "ğŸš€ Running apply for module: {{MODULE}}"
    @if [ -f "./examples/{{MODULE}}/{{EXAMPLE}}/{{FIXTURES_DIR}}/{{FIXTURE}}" ]; then \
        echo "ğŸ“„ Using fixture: {{FIXTURES_DIR}}/{{FIXTURE}} for apply"; \
        just tf-exec-nix "examples/{{MODULE}}/{{EXAMPLE}}" 'apply -var-file="{{FIXTURES_DIR}}/{{FIXTURE}}" -auto-approve'; \
    else \
        echo "ğŸ“„ No fixture provided, running apply without it"; \
        just tf-exec-nix "examples/{{MODULE}}/{{EXAMPLE}}" 'apply -auto-approve'; \
    fi

    @echo "ğŸ’£ Running destroy for module: {{MODULE}}"
    @if [ -f "./examples/{{MODULE}}/{{EXAMPLE}}/{{FIXTURES_DIR}}/{{FIXTURE}}" ]; then \
        echo "ğŸ“„ Using fixture: {{FIXTURES_DIR}}/{{FIXTURE}} for destroy"; \
        just tf-exec-nix "examples/{{MODULE}}/{{EXAMPLE}}" 'destroy -var-file="{{FIXTURES_DIR}}/{{FIXTURE}}" -auto-approve'; \
    else \
        echo "ğŸ“„ No fixture provided, running destroy without it"; \
        just tf-exec-nix "examples/{{MODULE}}/{{EXAMPLE}}" 'destroy -auto-approve'; \
    fi


# ğŸŒ€ Quick feedback loop for development in Nix environment
tf-dev-nix MODULE='default' EXAMPLE='basic' FIXTURE='default.tfvars' CLEAN='false':
    @echo "ğŸ”„ Cleaning up resources for module: {{MODULE}}, example: {{EXAMPLE}} (Clean: {{CLEAN}})"
    @if [ "{{CLEAN}}" = "true" ]; then \
        rm -rf "./modules/{{MODULE}}/.terraform" && \
        rm -rf "./examples/{{MODULE}}/{{EXAMPLE}}/.terraform" && \
        rm -f "./examples/{{MODULE}}/{{EXAMPLE}}/.terraform.lock.hcl"; \
        echo "âœ… Cleaned up resources for module: {{MODULE}}, example: {{EXAMPLE}}"; \
    else \
        echo "ğŸ›‘ No cleanup performed for module: {{MODULE}}, example: {{EXAMPLE}}"; \
    fi;

    @echo "ğŸ” Running CI checks for module: {{MODULE}}"
    @just tf-ci-static-nix "{{MODULE}}"

    @echo "ğŸ” Initializing module: {{MODULE}}"
    @just tf-cmd-nix "{{MODULE}}" 'init'

    @echo "ğŸ” Initializing example: {{EXAMPLE}} for module: {{MODULE}}"
    @just tf-exec-nix "examples/{{MODULE}}/{{EXAMPLE}}" 'init'

    @echo "ğŸ” Validating example: {{EXAMPLE}} for module: {{MODULE}}"
    @just tf-exec-nix "examples/{{MODULE}}/{{EXAMPLE}}" 'validate'

    @if [ -f "./examples/{{MODULE}}/{{EXAMPLE}}/{{FIXTURES_DIR}}/{{FIXTURE}}" ]; then \
        echo "ğŸ“„ Using fixture: {{FIXTURES_DIR}}/{{FIXTURE}} for planning"; \
        just tf-exec-nix "examples/{{MODULE}}/{{EXAMPLE}}" 'plan -var-file="{{FIXTURES_DIR}}/{{FIXTURE}}"'; \
    else \
        echo "ğŸ“„ No fixture provided, running plan without it"; \
        just tf-exec-nix "examples/{{MODULE}}/{{EXAMPLE}}" 'plan'; \
    fi

# ğŸ§ª Run unit tests - parameters: TAGS (E.g. 'readonly' or 'integration'), MOD (module name), NOCACHE (true/false), TIMEOUT (E.g. '60s|5m|1h')
tf-test-unit TAGS='readonly' MODULE='default' NOCACHE='true' TIMEOUT='60s':
    @echo "ğŸ§ª Running unit tests..."
    @echo "ğŸ“‹ Configuration:"
    @echo "   ğŸ·ï¸  Tags: unit,{{TAGS}}"
    @echo "   ğŸ” Module: {{MODULE}}"
    @echo "   ğŸ”„ No Cache: {{NOCACHE}}"
    @echo "   â±ï¸  Timeout: {{TIMEOUT}}"

    @if ! echo "{{TIMEOUT}}" | grep -qE '^[0-9]+[smh]$'; then \
        echo "âŒ Invalid timeout format. Use format like '60s', '5m', or '1h'"; \
        exit 1; \
    fi

    @cd {{TESTS_DIR}} && \
    if [ -z "{{MODULE}}" ] || [ "{{MODULE}}" = "default" ]; then \
        echo "ğŸ” Running unit tests for module: default in path {{TESTS_DIR}}/modules/{{MODULE}}/unit" && \
        echo "ğŸ§¹ Cleaning up terraform state" && \
        find . -type d -name ".terraform" -exec rm -rf {} \; 2>/dev/null || true; \
        find . -type f -name ".terraform.lock.hcl" -delete 2>/dev/null || true; \
        go test \
            -v \
            -tags "unit,{{TAGS}}" \
            $(if [ "{{NOCACHE}}" = "true" ]; then echo "-count=1"; fi) \
            -timeout="{{TIMEOUT}}" \
            ./modules/default/unit/...; \
    else \
        if [ -d "./modules/{{MODULE}}/unit" ]; then \
            echo "ğŸ” Running unit tests for module: {{MODULE}}" && \
            echo "ğŸ§¹ Cleaning up terraform state" && \
            find "./modules/{{MODULE}}/unit" -type d -name ".terraform" -exec rm -rf {} \; 2>/dev/null || true; \
            find "./modules/{{MODULE}}/unit" -type f -name ".terraform.lock.hcl" -delete 2>/dev/null || true; \
            go test \
                -v \
                -tags "unit,{{TAGS}}" \
                $(if [ "{{NOCACHE}}" = "true" ]; then echo "-count=1"; fi) \
                -timeout="{{TIMEOUT}}" \
                "./modules/{{MODULE}}/unit/..."; \
        else \
            echo "âŒ Unit test directory not found: ./modules/{{MODULE}}/unit"; \
            exit 1; \
        fi; \
    fi

# ğŸ§ª Run unit tests in Nix environment - parameters: TAGS (E.g. 'readonly' or 'integration'), MOD (module name), NOCACHE (true/false), TIMEOUT (E.g. '60s|5m|1h')
tf-test-unit-nix TAGS='readonly' MODULE='default' NOCACHE='true' TIMEOUT='60s':
    @echo "ğŸ§ª Running unit tests in Nix environment..."
    @echo "ğŸ“‹ Configuration:"
    @echo "   ğŸ·ï¸  Tags: unit,{{TAGS}}"
    @echo "   ğŸ” Module: {{MODULE}}"
    @echo "   ğŸ”„ No Cache: {{NOCACHE}}"
    @echo "   â±ï¸  Timeout: {{TIMEOUT}}"

    @if ! echo "{{TIMEOUT}}" | grep -qE '^[0-9]+[smh]$'; then \
        echo "âŒ Invalid timeout format. Use format like '60s', '5m', or '1h'"; \
        exit 1; \
    fi

    @nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command bash -c "cd {{TESTS_DIR}} && \
    if [ -z '{{MODULE}}' ] || [ '{{MODULE}}' = 'default' ]; then \
        echo 'ğŸ” Running unit tests for module: default' && \
        find . -type d -name '.terraform' -exec rm -rf {} \; 2>/dev/null || true; \
        find . -type f -name '.terraform.lock.hcl' -delete 2>/dev/null || true; \
        go test \
            -v \
            -tags 'unit,{{TAGS}}' \
            $(if [ '{{NOCACHE}}' = 'true' ]; then echo '-count=1'; fi) \
            -timeout='{{TIMEOUT}}' \
            ./modules/default/unit/...; \
    else \
        if [ -d './modules/{{MODULE}}/unit' ]; then \
            echo 'ğŸ” Running unit tests for module: {{MODULE}}' && \
            find './modules/{{MODULE}}/unit' -type d -name '.terraform' -exec rm -rf {} \; 2>/dev/null || true; \
            find './modules/{{MODULE}}/unit' -type f -name '.terraform.lock.hcl' -delete 2>/dev/null || true; \
            go test \
                -v \
                -tags 'unit,{{TAGS}}' \
                $(if [ '{{NOCACHE}}' = 'true' ]; then echo '-count=1'; fi) \
                -timeout='{{TIMEOUT}}' \
                './modules/{{MODULE}}/unit/...'; \
        else \
            echo 'âŒ Unit test directory not found: ./modules/{{MODULE}}/unit'; \
            exit 1; \
        fi; \
    fi"

# ğŸ§ª Run examples tests - parameters: TAGS (E.g. 'readonly' or 'integration'), MOD (module name), NOCACHE (true/false), TIMEOUT (E.g. '60s|5m|1h')
tf-test-examples TAGS='readonly' MODULE='default' NOCACHE='true' TIMEOUT='60s':
    @echo "ğŸ§ª Running example tests..."
    @echo "ğŸ“‹ Configuration:"
    @echo "   ğŸ·ï¸  Tags: examples,{{TAGS}}"
    @echo "   ğŸ” Module: {{MODULE}}"
    @echo "   ğŸ”„ No Cache: {{NOCACHE}}"
    @echo "   â±ï¸  Timeout: {{TIMEOUT}}"

    @if ! echo "{{TIMEOUT}}" | grep -qE '^[0-9]+[smh]$'; then \
        echo "âŒ Invalid timeout format. Use format like '60s', '5m', or '1h'"; \
        exit 1; \
    fi

    @cd {{TESTS_DIR}} && \
    if [ -z "{{MODULE}}" ] || [ "{{MODULE}}" = "default" ]; then \
        echo "ğŸ” Running examples tests for module: default in path {{TESTS_DIR}}/modules/{{MODULE}}/examples" && \
        echo "ğŸ§¹ Cleaning up terraform state" && \
        find . -type d -name ".terraform" -exec rm -rf {} \; 2>/dev/null || true; \
        find . -type f -name ".terraform.lock.hcl" -delete 2>/dev/null || true; \
        go test \
            -v \
            -tags "examples,{{TAGS}}" \
            $(if [ "{{NOCACHE}}" = "true" ]; then echo "-count=1"; fi) \
            -timeout="{{TIMEOUT}}" \
            ./modules/default/examples/...; \
    else \
        if [ -d "./modules/{{MODULE}}/examples" ]; then \
            echo "ğŸ” Running examples tests for module: {{MODULE}}" && \
            echo "ğŸ§¹ Cleaning up terraform state" && \
            find "./modules/{{MODULE}}/examples" -type d -name ".terraform" -exec rm -rf {} \; 2>/dev/null || true; \
            find "./modules/{{MODULE}}/examples" -type f -name ".terraform.lock.hcl" -delete 2>/dev/null || true; \
            go test \
                -v \
                -tags "examples,{{TAGS}}" \
                $(if [ "{{NOCACHE}}" = "true" ]; then echo "-count=1"; fi) \
                -timeout="{{TIMEOUT}}" \
                "./modules/{{MODULE}}/examples/..."; \
        else \
            echo "âŒ Examples test directory not found: ./modules/{{MODULE}}/examples"; \
            exit 1; \
        fi; \
    fi

# ğŸ§ª Run examples tests in Nix environment - parameters: TAGS (E.g. 'readonly' or 'integration'), MOD (module name), NOCACHE (true/false), TIMEOUT (E.g. '60s|5m|1h')
tf-test-examples-nix TAGS='readonly' MODULE='default' NOCACHE='true' TIMEOUT='60s':
    @echo "ğŸ§ª Running example tests in Nix environment..."
    @echo "ğŸ“‹ Configuration:"
    @echo "   ğŸ·ï¸  Tags: examples,{{TAGS}}"
    @echo "   ğŸ” Module: {{MODULE}}"
    @echo "   ğŸ”„ No Cache: {{NOCACHE}}"
    @echo "   â±ï¸  Timeout: {{TIMEOUT}}"

    @if ! echo "{{TIMEOUT}}" | grep -qE '^[0-9]+[smh]$'; then \
        echo "âŒ Invalid timeout format. Use format like '60s', '5m', or '1h'"; \
        exit 1; \
    fi

    @nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes --command bash -c "cd {{TESTS_DIR}} && \
    if [ -z '{{MODULE}}' ] || [ '{{MODULE}}' = 'default' ]; then \
        echo 'ğŸ” Running examples tests for module: default' && \
        find . -type d -name '.terraform' -exec rm -rf {} \; 2>/dev/null || true; \
        find . -type f -name '.terraform.lock.hcl' -delete 2>/dev/null || true; \
        go test \
            -v \
            -tags 'examples,{{TAGS}}' \
            $(if [ '{{NOCACHE}}' = 'true' ]; then echo '-count=1'; fi) \
            -timeout='{{TIMEOUT}}' \
            ./modules/default/examples/...; \
    else \
        if [ -d './modules/{{MODULE}}/examples' ]; then \
            echo 'ğŸ” Running examples tests for module: {{MODULE}}' && \
            find './modules/{{MODULE}}/examples' -type d -name '.terraform' -exec rm -rf {} \; 2>/dev/null || true; \
            find './modules/{{MODULE}}/examples' -type f -name '.terraform.lock.hcl' -delete 2>/dev/null || true; \
            go test \
                -v \
                -tags 'examples,{{TAGS}}' \
                $(if [ '{{NOCACHE}}' = 'true' ]; then echo '-count=1'; fi) \
                -timeout='{{TIMEOUT}}' \
                './modules/{{MODULE}}/examples/...'; \
        else \
            echo 'âŒ Examples test directory not found: ./modules/{{MODULE}}/examples'; \
            exit 1; \
        fi; \
    fi"

# ğŸ”¨ Build the Dagger pipeline
[working-directory:'pipeline/infra']
pipeline-infra-build:
    @echo "ğŸ”¨ Initializing Dagger development environment"
    @dagger develop
    @echo "ğŸ“‹ Building Dagger pipeline"
    @dagger functions

# ğŸ”¨ Help for Dagger job
[working-directory:'pipeline/infra']
pipeline-job-help fn: (pipeline-infra-build)
    @echo "ğŸ”¨ Help for Dagger job: {{fn}}"
    @dagger call {{fn}} --help

# ğŸ”¨ Open an interactive development shell for the Infra pipeline
[working-directory:'pipeline/infra']
pipeline-infra-shell args="": (pipeline-infra-build)
    @echo "ğŸš€ Launching interactive terminal"
    @dagger call open-terminal {{args}}

# ğŸ”¨ Execute a Dagger job with any arbitrary command
[working-directory:'pipeline/infra']
pipeline-job-exec mod="default" command="init" args="": (pipeline-infra-build)
    @echo "ğŸš€ Executing job: {{command}} with arguments: {{args}}"
    @dagger --use-hashicorp-image=true call job-terraform-exec \
       --command="{{command}}" \
       --tf-module-path="{{mod}}" \
       --arguments="{{args}}" \
       --load-dot-env-file=true \
       --no-cache=true \
       --git-ssh $SSH_AUTH_SOCK

# ğŸ”¨ Validate Terraform modules for best practices and security
[working-directory:'pipeline/infra']
pipeline-job-terraform-static-check MODULE="default" args="": (pipeline-infra-build)
    @echo " Analyzing Terraform modules for security and best practices"
    @echo "âš¡ Running static analysis checks"
    @dagger --use-hashicorp-image=true call job-terraform-static-check \
       --tf-module-path="{{MODULE}}" \
       --load-dot-env-file=true \
       --no-cache=true \
       --git-ssh $SSH_AUTH_SOCK
    @echo "âœ… Static analysis completed successfully"

# ğŸ”¨ Test Terraform modules against multiple provider versions
[working-directory:'pipeline/infra']
pipeline-job-terraform-version-compatibility-check MODULE="default": (pipeline-infra-build)
    @echo " Testing module compatibility across provider versions"
    @echo "âš¡ Running version compatibility checks"
    @dagger --use-hashicorp-image=true call job-terraform-version-compatibility-check \
       --tf-module-path="{{MODULE}}" \
       --load-dot-env-file=true \
       --no-cache=true \
       --git-ssh $SSH_AUTH_SOCK
    @echo "âœ… Version compatibility testing completed"

# ğŸ”¨ Run comprehensive CI checks for Terraform modules
pipeline-infra-tf-ci MODULE="default" args="": (pipeline-job-terraform-static-check MODULE args) (pipeline-job-terraform-version-compatibility-check MODULE)
